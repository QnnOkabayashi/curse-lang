use crate::{lexer::*, Parser};
use curse_ast::ast::{
    tok, Appl, Arm, ChoiceDef, Closure, Constructor, Expr, Field, FunctionDef,
    GenericArgs, GenericParams, Lit, NamedType, Param, Paren, Path, Pat, Program, Record, StructDef, 
    Symbol, Type, VariantDef, Variants,
    Region, RegionKind, bikeshed,
};
use curse_interner::Ident;
use curse_span::HasSpan;

grammar<'input>(parser: &mut Parser<'_>);

extern {
    type Location = usize;
    type Error = LexError;

    // Any changes here must also be reflected in:
    // - curse_parse/src/lexer.rs
    // - curse_ast/src/tok.rs
    enum Token<'input> {
        // In the grammar, if we specify the string on the lhs,
        // it means that we're waiting for the lexer to
        // give us the rhs.
        "ident literal" => Token::Ident(<tok::Literal<'input>>),
        "type ident literal" => Token::TypeIdent(<tok::Literal<'input>>),
        "integer literal" => Token::Integer(<tok::Literal<'input>>),
        "string literal" => Token::StringLiteral(<tok::Literal<'input>>),
        ":" => Token::Colon(<tok::Colon>),
        "::" => Token::ColonColon(<tok::ColonColon>),
        "," => Token::Comma(<tok::Comma>),
        "(" => Token::LParen(<tok::LParen>),
        ")" => Token::RParen(<tok::RParen>),
        "+" => Token::Plus(<tok::Plus>),
        "-" => Token::Minus(<tok::Minus>),
        "*" => Token::Star(<tok::Star>),
        "." => Token::Dot(<tok::Dot>),
        ".." => Token::DotDot(<tok::DotDot>),
        ";" => Token::Semi(<tok::Semi>),
        "%" => Token::Percent(<tok::Percent>),
        "/" => Token::Slash(<tok::Slash>),
        "|" => Token::Pipe(<tok::Pipe>),
        "fn" => Token::Fn(<tok::Fn>),
        "choice" => Token::Choice(<tok::Choice>),
        "struct" => Token::Struct(<tok::Struct>),
        "{" => Token::LBrace(<tok::LBrace>),
        "}" => Token::RBrace(<tok::RBrace>),
        "->" => Token::Arrow(<tok::Arrow>),

        "=" => Token::Eq(<tok::Eq>),
        "<" => Token::Lt(<tok::Lt>),
        ">" => Token::Gt(<tok::Gt>),
        "<=" => Token::Le(<tok::Le>),
        ">=" => Token::Ge(<tok::Ge>),

        "true" => Token::True(<tok::True>),
        "false" => Token::False(<tok::False>),
        "ref" => Token::Ref(<tok::Ref>),
        "mut" => Token::Mut(<tok::Mut>),
        "dynamic_import" => Token::DynamicImport(<tok::DynamicImport>),
    }
}

/// === Term Aliases ===

// TODO(quinn): make this not hard to read
Ident: Ident = "ident literal" => Ident::new_in(<>.as_ref(), <>.span(), parser.interner);
TypeIdent: Ident = "type ident literal" => Ident::new_in(<>.as_ref(), <>.span(), parser.interner);
Integer: Ident = "integer literal" => Ident::new_in(<>.as_ref(), <>.span(), parser.interner);

// TODO(quinn): make this not an `Ident` so it's more descriptive
// But we do want it interned to dedup string literals eventually
StringLiteral: Ident = "string literal" => Ident::new_in(<>.as_ref(), <>.span(), parser.interner);


/// === Program ===

pub Program: Program = {
    FunctionDef => Program::default().with_function_def(<>),
    ChoiceDef => Program::default().with_choice_def(<>),
    StructDef => Program::default().with_struct_def(<>),
    DynamicImport => Program::default().with_dynamic_import(<>),

    Program FunctionDef => Program::with_function_def(<>),
    Program ChoiceDef => Program::with_choice_def(<>),
    Program StructDef => Program::with_struct_def(<>),
    Program DynamicImport => Program::with_dynamic_import(<>),

    ! => {
        parser.errors.push(<>.error.into());
        Program::default()
    }
};

/// === Definitions ===

// TODO(quinn): BAD BAD BAD THIS IS BASICALLY C INCLUDE
DynamicImport: bikeshed::DynamicImport = {
    "dynamic_import" Ident => bikeshed::DynamicImport::new(<>),
};

GenericParams: GenericParams = {
    "|" (TypeIdent "*")* TypeIdent "|" => GenericParams::new(<>),
};

// ExplicitTypes: ExplicitTypes = {
//     GenericParams? ":" Ref<Type> => ExplicitTypes::new(<>),
// }

FunctionDef: FunctionDef = {
    "fn" Ident Closure => FunctionDef::new(<>),
    // "fn" Ident ExplicitTypes? Closure => FunctionDef::new(<>),
};

StructDef: StructDef = {
    "struct" TypeIdent GenericParams? Type => StructDef::new(<>),
};

ChoiceDef: ChoiceDef = {
    "choice" TypeIdent GenericParams? Variants => ChoiceDef::new(<>),
};

Variants: Variants = {
    "{" (VariantDef ",")* VariantDef? "}" => Variants::new(<>),
}

VariantDef: VariantDef = {
    TypeIdent Type => VariantDef::new(<>),
};


/// === Record ===

Record<T>: Record<T> = {
    "{" (Field<T> ",")* Field<T>? "}" => Record::new(<>),
};

Field<T>: Field<T> = {
    Ident (":" T)? => Field::new(<>),
};


/// === Types ===

Type: Type = {
    NamedType => Type::Named(Box::new(<>)),
    Record<Type> => Type::Record(Box::new(<>)),
    ! => {
        parser.errors.push(<>.error.into());
        Type::Error
    }
};

GenericArgs: GenericArgs = {
    Type => GenericArgs::Single(<>),
    "(" (Type "*")* Type ")" => GenericArgs::CartesianProduct(<>),
};

NamedType: NamedType = {
    TypePath GenericArgs? => NamedType::new(<>),
};


/// === Patterns ===

Pat: Pat = {
    Lit => Pat::Lit(<>),
    Record<Pat> => Pat::Record(Box::new(<>)),
    Constructor<Pat> => Pat::Constructor(Box::new(<>)),
};


/// === Shared ===

// Can be used in patterns or as expressions
Lit: Lit = {
    // TODO(quinn): use the `StringLiteral` rule for string literals once we add those
    Integer => Lit::Integer(<>),
    Ident => Lit::Ident(<>),
    "true" => Lit::True(<>),
    "false" => Lit::False(<>),
};

Path: Path = {
    (Ident "::")* Ident => Path::new(<>),
};

TypePath: Path = {
    (Ident "::")* TypeIdent => Path::new(<>),
};

Constructor<T>: Constructor<T> = {
    TypePath ("::" TypeIdent)? T => Constructor::new(<>),
};


/// === Expressions ===

Symbol: Symbol = {
    "+" => Symbol::Plus(<>),
    "-" => Symbol::Minus(<>),
    "*" => Symbol::Star(<>),
    "." => Symbol::Dot(<>),
    ".." => Symbol::DotDot(<>),
    ";" => Symbol::Semi(<>),
    "%" => Symbol::Percent(<>),
    "/" => Symbol::Slash(<>),
    "=" => Symbol::Eq(<>),
    "<" => Symbol::Lt(<>),
    ">" => Symbol::Gt(<>),
    "<=" => Symbol::Le(<>),
    ">=" => Symbol::Ge(<>),
};

ClosureNonpiecewise: Closure = {
    Arm => Closure::NonPiecewise(<>),
};

ClosurePiecewise: Closure = {
    "(" ")" => Closure::Empty(<>),
    "(" (Arm ",")+ Arm? ")" => Closure::Piecewise(<>),
};

Closure = {
    ClosureNonpiecewise,
    ClosurePiecewise,
};

Arm: Arm = {
    "|" (Param ",")* Param? "|" EndExpr => Arm::new(<>),
};

Param: Param = {
    Pat (":" Type)? => Param::new(<>),
};

Appl<Rhs>: Appl = {
    Expr Term Rhs => Appl::new(<>),
};

Region: Region = {
    RegionKind Pat "{" EndExpr "}" => Region::new(<>),
};

RegionKind: RegionKind = {
    "ref" => RegionKind::Ref(<>),
    "mut" => RegionKind::Mut(<>),
    "ref" "mut" => RegionKind::RefMut(<>),
}

Paren: Paren = {
    "(" EndExpr ")" => Paren::new(<>),
};

Term: Expr = {
    Paren => Expr::Paren(Box::new(<>)),
    Symbol => Expr::Symbol(<>),
    Lit => Expr::Lit(<>),
    Record<EndExpr> => Expr::Record(Box::new(<>)),
    ClosurePiecewise => Expr::Closure(Box::new(<>)),
    Region => Expr::Region(Box::new(<>)),
    Constructor<Term> => Expr::Constructor(Box::new(<>)),
    ! => {
        parser.errors.push(<>.error.into());
        Expr::Error
    }
};

EndTerm = {
    Term,
    TermThatAbsorbsEverythingAfter,
};

TermThatAbsorbsEverythingAfter: Expr = {
    // e.g. |x| ...
    ClosureNonpiecewise => Expr::Closure(Box::new(<>)),
    // e.g. Some Ok |x| ...
    Constructor<TermThatAbsorbsEverythingAfter> => Expr::Constructor(Box::new(<>)),
};

Expr = {
    Term,
    Appl<Term> => Expr::Appl(Box::new(<>)),
};

pub EndExpr = {
    EndTerm,
    Appl<EndTerm> => Expr::Appl(Box::new(<>)),
};
