use crate::{lexer::*, Parser};
use curse_ast::{
    tok, Appl, Arm, ChoiceDef, Closure, Constructor, ExplicitTypes, Expr, Field, FunctionDef,
    GenericArgs, GenericParams, Lit, NamedType, Param, Paren, Pat, Program, Record, StructDef,
    Symbol, Type, VariantDef, Variants,
};

grammar<'ast, 'input>(parser: &mut Parser<'ast, 'input>);

extern {
    type Location = usize;
    type Error = LexError;

    // Any changes here must also be reflected in:
    // - curse_parse/src/lexer.rs
    // - curse_ast/src/tok.rs
    enum Token<'input> {
        // In the grammar, if we specify the string on the lhs,
        // it means that we're waiting for the lexer to
        // give us the rhs.
        "ident literal" => Token::Ident(<tok::Ident<'input>>),
        "integer literal" => Token::Integer(<tok::Integer<'input>>),
        "named type literal" => Token::TypeIdent(<tok::TypeIdent<'input>>),
        ":" => Token::Colon(<tok::Colon>),
        "," => Token::Comma(<tok::Comma>),
        "(" => Token::LParen(<tok::LParen>),
        ")" => Token::RParen(<tok::RParen>),
        "+" => Token::Plus(<tok::Plus>),
        "-" => Token::Minus(<tok::Minus>),
        "*" => Token::Star(<tok::Star>),
        "." => Token::Dot(<tok::Dot>),
        ".." => Token::DotDot(<tok::DotDot>),
        ";" => Token::Semi(<tok::Semi>),
        "%" => Token::Percent(<tok::Percent>),
        "/" => Token::Slash(<tok::Slash>),
        "|" => Token::Pipe(<tok::Pipe>),
        "fn" => Token::Fn(<tok::Fn>),
        "struct" => Token::Struct(<tok::Struct>),
        "choice" => Token::Choice(<tok::Choice>),
        "{" => Token::LBrace(<tok::LBrace>),
        "}" => Token::RBrace(<tok::RBrace>),
        "->" => Token::Arrow(<tok::Arrow>),

        "=" => Token::Eq(<tok::Eq>),
        "<" => Token::Lt(<tok::Lt>),
        ">" => Token::Gt(<tok::Gt>),
        "<=" => Token::Le(<tok::Le>),
        ">=" => Token::Ge(<tok::Ge>),

        "true" => Token::True(<tok::True>),
        "false" => Token::False(<tok::False>),
    }
}

/// === Term Aliases ===

Ident: tok::Ident<'input> = "ident literal";

Integer: tok::Integer<'input> = "integer literal";

TypeIdent: tok::TypeIdent<'input> = "named type literal";


/// === Program ===

pub Program: Program<'ast, 'input> = {
    FunctionDef => Program::default().with_function_def(<>),
    ChoiceDef => Program::default().with_choice_def(<>),
    StructDef => Program::default().with_struct_def(<>),

    Program FunctionDef => Program::with_function_def(<>),
    Program ChoiceDef => Program::with_choice_def(<>),
    Program StructDef => Program::with_struct_def(<>),

    ! => {
        parser.errors.push(<>.error.into());
        Program::default()
    }
};

/// === Definitions ===

GenericParams: GenericParams<'input> = {
    TypeIdent => GenericParams::Single(<>),
    "(" (TypeIdent "*")* TypeIdent ")" => GenericParams::CartesianProduct(<>),
};

ExplicitTypes: ExplicitTypes<'ast, 'input> = {
    GenericParams? ":" Ref<Type> => ExplicitTypes::new(<>),
}

FunctionDef: FunctionDef<'ast, 'input> = {
    "fn" Ident ExplicitTypes? "=" Closure => FunctionDef::new(<>),
};

StructDef: StructDef<'ast, 'input> = {
    "struct" TypeIdent GenericParams? "=" Ref<Type> => StructDef::new(<>),
};

ChoiceDef: ChoiceDef<'ast, 'input> = {
    "choice" TypeIdent GenericParams? "=" Variants => ChoiceDef::new(<>),
};

Variants: Variants<'ast, 'input> = {
    "|" => Variants::Never(<>),
    "|"? (VariantDef "|")* VariantDef => Variants::Variants(<>),
}

VariantDef: VariantDef<'ast, 'input> = {
    TypeIdent Ref<Type> => VariantDef::new(<>),
};


/// === Record ===

Record<T>: Record<'input, T> = {
    "{" (Field<T> ",")* Field<T>? "}" => Record::new(<>),
};

Field<T>: Field<'input, T> = {
    Ident (":" T)? => Field::new(<>),
};

// Get an arena-allocated reference
Ref<T>: &'ast T = {
    T => &*parser.arena.alloc(<>),
}

/// === Types ===

Type: Type<'ast, 'input> = {
    NamedType => Type::Named(<>),
    Record<Ref<Type>> => Type::Record(<>),
    ! => {
        parser.errors.push(<>.error.into());
        Type::Error
    }
};

GenericArgs: GenericArgs<'ast, 'input> = {
    Ref<Type> => GenericArgs::Single(<>),
    "(" (Type "*")* Ref<Type> ")" => GenericArgs::CartesianProduct(<>),
};

NamedType: NamedType<'ast, 'input> = {
    TypeIdent GenericArgs? => NamedType::new(<>),
};


/// === Patterns ===

Pat: Pat<'ast, 'input> = {
    Lit => Pat::Lit(<>),
    Record<Ref<Pat>> => Pat::Record(<>),
    TypeIdent Ref<Pat> => Pat::Struct(<>),
    // TODO(quinn): add choice pats and qpaths
};


/// === Expressions ===

Symbol: Symbol = {
    "+" => Symbol::Plus(<>),
    "-" => Symbol::Minus(<>),
    "*" => Symbol::Star(<>),
    "." => Symbol::Dot(<>),
    ".." => Symbol::DotDot(<>),
    ";" => Symbol::Semi(<>),
    "%" => Symbol::Percent(<>),
    "/" => Symbol::Slash(<>),
    "=" => Symbol::Eq(<>),
    "<" => Symbol::Lt(<>),
    ">" => Symbol::Gt(<>),
    "<=" => Symbol::Le(<>),
    ">=" => Symbol::Ge(<>),
};

Lit: Lit<'input> = {
    Integer => Lit::Integer(<>),
    Ident => Lit::Ident(<>),
    "true" => Lit::True(<>),
    "false" => Lit::False(<>),
};

ClosureNonpiecewise: Closure<'ast, 'input> = {
    Arm => Closure::NonPiecewise(<>),
};

ClosurePiecewise: Closure<'ast, 'input> = {
    "(" ")" => Closure::Empty(<>),
    "(" (Arm ",")+ Arm? ")" => Closure::Piecewise(<>),
};

Closure = {
    ClosureNonpiecewise,
    ClosurePiecewise,
};

Arm: Arm<'ast, 'input> = {
    "|" (Param ",")* Param? "|" Ref<EndExpr> => Arm::new(<>),
};

Param: Param<'ast, 'input> = {
    Ref<Pat> (":" Ref<Type>)? => Param::new(<>),
};

Constructor: Constructor<'ast, 'input> = {
    TypeIdent Ref<EndExpr> => Constructor::new(<>),
};

Appl<Rhs>: Appl<'ast, 'input> = {
    Ref<Expr> Ref<Term> Ref<Rhs> => Appl::new(<>),
};

Paren: Paren<'ast, 'input> = {
    "(" Ref<EndExpr> ")" => Paren::new(<>),
};

Term: Expr<'ast, 'input> = {
    Paren => Expr::Paren(<>),
    Symbol => Expr::Symbol(<>),
    Lit => Expr::Lit(<>),
    Record<Ref<EndExpr>> => Expr::Record(<>),
    ClosurePiecewise => Expr::Closure(<>),
    ! => {
        parser.errors.push(<>.error.into());
        Expr::Error
    }
};

EndTerm = {
    Term,
    Constructor => Expr::Constructor(<>),
    ClosureNonpiecewise => Expr::Closure(<>),
};

Expr = {
    Term,
    Appl<Term> => Expr::Appl(<>),
};

pub EndExpr = {
    EndTerm,
    Appl<EndTerm> => Expr::Appl(<>),
};
